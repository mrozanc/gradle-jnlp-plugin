/*
 * Copyright 2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// configuration of the Maven artifacts
apply plugin: 'maven-publish'

import java.text.SimpleDateFormat
Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

jar {
    manifest {
        attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': System.properties['java.version'] + ' (' + System.properties['java.vendor'] + ' ' + System.properties["java.vm.version"] + ')',
            'Build-Date': project.buildDate,
            'Build-Time': project.buildTime,
            'Specification-Title': project.name,
            'Specification-Version': project.version.toString(),
            'Specification-Vendor': 'Tobias Schulte',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version.toString(),
            'Implementation-Vendor': 'Tobias Schulte'
        )
    }
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

def pomConfig = {
    name project.name
    description 'Gradle plugin to generate webstart and applet jnlp files'
    url 'https://github.com/tschulte/gradle-jnlp-plugin'
    inceptionYear '2014'
    licenses {
        license([:]) {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/tschulte/gradle-jnlp-plugin'
    }
    developers {
        [
            tschulte : 'Tobias Schulte'
        ].each { devId, devName ->
            developer {
                id devId
                name devName
                roles {
                    role 'Developer'
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

// we cannot directly depend on generatePomFileForMavenJavaPublication in releaseAsset.builtBy
task generatePomFile(dependsOn: ['generatePomFileForMavenJavaPublication'])

rootProject.semanticRelease {
    repo {
        releaseAsset jar
        releaseAsset sourcesJar
        releaseAsset javadocJar
        releaseAsset file("$buildDir/publications/mavenJava/pom-default.xml"),
                    name: "${name}-${version}.pom",
                    contentType: "application/x-maven-pom+xml",
                    builtBy: generatePomFile
    }
}
